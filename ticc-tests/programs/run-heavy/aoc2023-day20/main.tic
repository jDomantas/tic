import ints from "std/int";
import option(Option, Some, None) from "std/option";
import result(Result, Ok, Err) from "std/result";
import list(List, Nil, Cons) from "std/list";
import strings from "std/string";
import array(Array) from "std/array";
import utils from "../shared/aoc-utils.tic";
import parse(Parse) from "../shared/parse.tic";

type Kind = Broadcaster | FlipFlop | Con | One;

type Module = Module Kind string (List string);

let parseDests : Parse (List string) =
    parse.remainder
    |> parse.map (\x ->
        x
        |> strings.split (strings.charAt 0 ",")
        |> list.map strings.trim);

let parseModule : string -> Result Module string =
    let parseKind =
        parse.literal "broadcaster"
        |> parse.map (\l -> Module Broadcaster l)
        |> parse.or (
            parse.ok (Module FlipFlop)
            |> parse.ignore (parse.literal "%")
            |> parse.apply parse.word)
        |> parse.or (
            parse.ok (Module Con)
            |> parse.ignore (parse.literal "&")
            |> parse.apply parse.word);
    \l ->
        parseKind
        |> parse.ignore (parse.literal " -> ")
        |> parse.apply parseDests
        |> parse.run l;

let parseInput : string -> Result (List Module) string =
    \x ->
        x
        |> strings.lines
        |> list.map parseModule
        |> list.allOk;

type ModuleState =
    | FlipFlopOff (List int)
    | FlipFlopOn (List int)
    | Conjunction (List int) (List int)
    | Onejunction (List int)
    | Broadcast (List int);

let rxIdx : int = 6669990;

let findIdx : string -> List Module -> Result int string =
    let go = \name -> \fold modules -> \idx ->
        match modules with
        | Nil -> None
        | Cons x xs ->
            match x with
            | Module _ n _ ->
                if strings.equal n name then
                    Some idx
                else
                    xs (idx + 1)
            end
        end;
    \name -> \modules ->
        if strings.equal name "rx" then
            Ok rxIdx
        else
            go name modules 0
            |> result.okOr (strings.concat name " does not exist");

let resolveDsts : List Module -> List string -> Result (List int) string =
    \all -> \dsts ->
        dsts
        |> list.map (\n -> findIdx n all)
        |> list.allOk;

let findInputs : List Module -> string -> List int =
    let go = \name -> \fold modules -> \idx ->
        match modules with
        | Nil -> Nil
        | Cons x xs ->
            match x with
            | Module _ _ dsts ->
                let isInput =
                    dsts
                    |> list.map (\x -> strings.equal x name)
                    |> list.any;
                if isInput then
                    Cons idx (xs (idx + 1))
                else
                    xs (idx + 1)
            end
        end;
    \all -> \to -> go to all 0;

let makeState : List Module -> Module -> Result ModuleState string =
    \all -> \m ->
        match m with
        | Module kind name dsts ->
            match resolveDsts all dsts with
            | Err e -> Err e
            | Ok dsts ->
                match kind with
                | Broadcaster -> Ok (Broadcast dsts)
                | FlipFlop -> Ok (FlipFlopOff dsts)
                | Con ->
                    let inputs = findInputs all name;
                    Ok (Conjunction inputs dsts)
                | One -> Ok (Onejunction dsts)
                end
            end
        end;

type Signal = Low | High;
type Message = Message Signal int int;
type Pair a b = Pair a b;
type Action = SendHigh (List int) | SendLow (List int) | NoAction;

let handleMessage : Array ModuleState -> Message -> Pair (Array ModuleState) (List Message) =
    \modules -> \msg ->
        match msg with
        | Message signal src dst ->
            match array.get dst modules with
            | Some module ->
                let res =
                    match module with
                    | FlipFlopOff dsts ->
                        match signal with
                        | High -> Pair (FlipFlopOff dsts) NoAction
                        | Low -> Pair (FlipFlopOn dsts) (SendHigh dsts)
                        end
                    | FlipFlopOn dsts ->
                        match signal with
                        | High -> Pair (FlipFlopOn dsts) NoAction
                        | Low -> Pair (FlipFlopOff dsts) (SendLow dsts)
                        end
                    | Broadcast dsts ->
                        match signal with
                        | High -> Pair module (SendHigh dsts)
                        | Low -> Pair module (SendLow dsts)
                        end
                    | Conjunction lows dsts ->
                        -- let src = debugIf (dst == 4) "con received signal from" src;
                        -- let signal = debugIf (dst == 4) "con received signal" signal;
                        -- let lows = debugIf (dst == 4) "con lows before" lows;
                        let lows =
                            match signal with
                            | High -> lows |> list.filter (\x -> x != src)
                            | Low -> Cons src lows
                            end;
                        -- let lows = debugIf (dst == 4) "con lows after" lows;
                        if list.length lows == 0 then
                            Pair (Conjunction lows dsts) (SendLow dsts)
                        else
                            Pair (Conjunction lows dsts) (SendHigh dsts)
                    | Onejunction dsts ->
                        match signal with
                        | High -> Pair (Onejunction dsts) (SendLow dsts)
                        | Low -> Pair (Onejunction dsts) (SendHigh dsts)
                        end
                    end;
                match res with
                | Pair newState action ->
                    let modules = array.set dst newState modules;
                    let messages =
                        match action with
                        | NoAction -> Nil
                        | SendHigh to -> list.map (\i -> Message High dst i) to
                        | SendLow to -> list.map (\i -> Message Low dst i) to
                        end;
                    Pair modules messages
                end
            | None -> Pair modules Nil
            end
        end;

type State = State (Array ModuleState) (List Message) (List Message);

let step : State -> State =
    \state ->
        match state with
        | State modules messages all ->
            match messages with
            | Nil -> state
            | Cons x xs ->
                match handleMessage modules x with
                | Pair modules newMessages ->
                    let messages = list.concat xs newMessages;
                    State modules messages (Cons x all)
                end
            end
        end;

let sendAndPropagate : State -> Message -> Result State string =
    \state -> \msg ->
        match state with
        | State modules _ handled ->
            let state = State modules (Cons msg Nil) handled;
            let state = ints.iterate 1000 step state;
            match state with
            | State _ pending _ ->
                if list.length pending > 0 then
                    Err "button press resulted in more than 1000 messages"
                else
                    Ok state
            end
        end;

let solve : Array ModuleState -> int -> Result (List Message) string =
    \modules -> \start ->
        let msg = Message Low 80085 start;
        let state = State modules (Cons msg Nil) Nil;
        let state = ints.iterate 1000 (\s -> result.andThen (\s -> sendAndPropagate s msg) s) (Ok state);
        match state with
        | Ok state ->
            match state with
            | State _ _ handled ->
                Ok (list.reverse handled)
            end
        | Err e -> Err e
        end;

let nthName : List Module -> int -> string =
    \m -> \idx ->
        if idx == 80085 then
            "button"
        else if idx == rxIdx then
            "rx"
        else
            m
            |> list.skip idx
            |> list.head
            |> option.map (\m -> match m with | Module _ n _ -> n end)
            |> option.unwrapOr "unknown";

let showMessage : List Module -> Message -> string =
    let append = \a -> \b -> strings.concat b a;
    \all -> \msg ->
        match msg with
        | Message kind src dst ->
            let kind =
                match kind with
                | High -> "high"
                | Low -> "low"
                end;
            nthName all src
            |> append " -"
            |> append kind
            |> append "-> "
            |> append (nthName all dst)
        end;

let part1 : List Module -> string =
    \modules ->
        let states = modules |> list.map (makeState modules) |> list.allOk;
        let start = findIdx "broadcaster" modules;
        states
        |> result.andThen (\states ->
            start
            |> result.map (\start ->
                match solve (array.fromList states) start with
                | Ok msgs ->
                    -- msgs
                    -- |> list.map (showMessage modules)
                    -- |> list.map (strings.concat (strings.fromChar 10))
                    -- |> list.join ""
                    let isHigh = \m ->
                        match m with
                        | Message kind _ _ ->
                            match kind with
                            | High -> true
                            | Low -> false
                            end
                        end;
                    let high =
                        msgs
                        |> list.filter isHigh
                        |> list.length;
                    let low = list.length msgs - high;
                    low * high
                    |> ints.toString
                | Err e -> e
                end))
        |> result.join;

type FindState = FindSearch State Message int | Found int | Failed string;

let step : FindState -> FindState =
    \s ->
        match s with
        | Failed _ -> s
        | Found _ -> s
        | FindSearch s m i ->
            let state = sendAndPropagate s m;
            match state with
            | Err e -> Failed e
            | Ok state ->
                match state with
                | State modules _ handled ->
                    let done =
                        handled
                        |> list.filter (\m ->
                            match m with
                            | Message kind _ dst ->
                                if dst == rxIdx then
                                    match kind with
                                    | High -> false
                                    | Low -> true
                                    end
                                else
                                    false
                            end)
                        |> list.length
                        |> (\x -> x > 0);
                    if done then
                        Found (i + 1)
                    else
                        FindSearch (State modules Nil Nil) m (i + 1)
                end
            end
        end;

let solve : Array ModuleState -> int -> Result int string =
    \modules -> \start ->
        let msg = Message Low 80085 start;
        let state = State modules (Cons msg Nil) Nil;
        let state = FindSearch state msg 0;
        match ints.iterate 5000 step state with
        | FindSearch _ _ _ -> Err "rx did not receive low pulse"
        | Failed e -> Err e
        | Found x -> Ok x
        end;

let extractBroadcaster : List Module -> Result (Pair Module (List Module)) string =
    \modules ->
        let broadcaster =
            modules
            |> list.filter (\m ->
                match m with
                | Module _ name _ -> strings.equal name "broadcaster"
                end)
            |> list.head
            |> result.okOr "no broadcaster";
        let rest =
            modules
            |> list.filter (\m ->
                match m with
                | Module _ name _ ->
                    if strings.equal name "broadcaster" then
                        false
                    else
                        true
                end);
        broadcaster
        |> result.map (\b -> Pair b rest);

let splitSubtasks : List Module -> Result (List (List Module)) string =
    \modules ->
        match extractBroadcaster modules with
        | Err e -> Err e
        | Ok p ->
            match p with
            | Pair broadcaster rest ->
                let rest =
                    rest
                    |> list.map (\m ->
                        match m with
                        | Module _ name dsts ->
                            let isFinal =
                                dsts
                                |> list.map (strings.equal "rx")
                                |> list.any;
                            if isFinal then
                                Module One name dsts
                            else
                                m
                        end);
                    match broadcaster with
                    | Module kind name dsts ->
                        dsts
                        |> list.map (\d ->
                            let broadcaster = Module kind name (Cons d Nil);
                            Cons broadcaster rest)
                        |> Ok
                    end
            end
        end;

let solveTask : List Module -> Result int string =
    \modules ->
        let states = modules |> list.map (makeState modules) |> list.allOk;
        let start = findIdx "broadcaster" modules;
        states
        |> result.andThen (\states ->
            start
            |> result.andThen (\start ->
                solve (array.fromList states) start));

let part2 : List Module -> string =
    \modules ->
        match splitSubtasks modules with
        | Err e -> e
        | Ok tasks ->
            tasks
            |> list.map solveTask
            |> list.allOk
            |> result.map (list.foldl 1 ints.lcm)
            |> result.map ints.toString
            |> result.join
        end;

export let main : string -> string = utils.makeSolution parseInput part1 part2;
